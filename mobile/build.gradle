apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.damn.uglass"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "CLIENT_ID", "\"${loadSecret("UBER_CLIENT_ID")}\"" //Add your client id to gradle.properties
        buildConfigField "String", "REDIRECT_URI", "\"${loadSecret("UBER_REDIRECT_URI")}\"" //Add your redirect uri to gradle.properties
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.uber.sdk:rides-android:0.5.3'
    implementation project(path: ':shared')
}

/**
 * Loads property from gradle.properties and ~/.gradle/gradle.properties
 * Use to look up confidential information like keys that shoudln't be stored publicly
 * @param name to lookup
 * @return the value of the property
 */
def loadSecret(String name) {
    if (!project.rootProject.file('uber.properties').canRead())
        throw new GradleException("Could not read 'uber.properties'!")
    Properties properties = new Properties()
    properties.load(project.rootProject.file('uber.properties').newDataInputStream())
    return properties.getProperty(name)
}
